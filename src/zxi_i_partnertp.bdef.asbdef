managed implementation in class zbp_xi_i_partnertp unique;
strict ( 1 );
with draft;

define behavior for ZXI_I_PartnerTP alias Partner
persistent table zxipartner
draft table zxipartnerdr
lock master total etag ChangedAt
authorization master ( instance )
etag master ChangedAt
{
  field ( numbering : managed ) Uuid;
  field ( readonly ) Uuid, Partner, IsLocked, CreatedAt, CreatedBy, ChangedAt, ChangedBy;
  field ( mandatory ) FirstName, LastName;

  action ( features : instance ) partnerLock result [1] $self;
  action ( features : instance ) partnerUnlock result [1] $self;

  validation validatePartner on save { create; update; field FirstName; }
  determination determinePartner on save { create; }
  create ( precheck );
  update ( precheck );
  delete;


  draft action Edit;
  draft action Resume;
  draft action Discard;
  draft action Activate optimized;
  draft determine action Prepare { validation validatePartner; }

  association _PartnerRoles { create; with draft; }

  mapping for zxipartner
    {
      Uuid          = guid;
      Partner       = partner;
      Title         = title;
      AcademicTitle = acad_title;
      FirstName     = name_first;
      LastName      = name_last;
      IsLocked      = locked;
      CreatedBy     = created_by;
      CreatedAt     = created_at;
      ChangedBy     = changed_by;
      ChangedAt     = changed_at;
    }
}

define behavior for ZXI_I_PartnerRoleTP alias PartnerRole
persistent table zxipartnerrole
draft table zxipartnerroledr
lock dependent by _Partner
authorization dependent by _Partner
etag master ChangedAt
{
  field ( numbering : managed ) Uuid;
  field ( readonly ) Uuid, ParentUuid, CreatedAt, CreatedBy, ChangedAt, ChangedBy;
  determination determineRole on save { create; }
  update;
  delete;

  association _Partner { with draft; }

  mapping for zxipartnerrole
    {
      Uuid        = GUID;
      PartnerRole = PARTNERROLE;
      CreatedAt   = created_at;
      CreatedBy   = created_by;
      ChangedAt   = changed_at;
      ChangedBy   = changed_by;
      ParentUuid  = PARENT_GUID;
    }
}