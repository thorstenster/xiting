managed implementation in class zbp_xi_i_partnertp unique;
strict ( 2 );

define behavior for ZXI_I_PartnerTP alias Partner
persistent table zxipartner
lock master
authorization master ( instance )
etag master TimeStamp
{
  field ( numbering : managed ) Uuid;
  field ( readonly ) Uuid, TimeStamp;

  create;
  update;
  delete;
  association _PartnerRoles { create; }

  mapping for zxipartner
    {
      Partner       = partner;
      AcademicTitle = acad_title;
      Title         = title;
      FirstName     = name_first;
      LastName      = name_last;
      TimeStamp     = timestamp;
      Uuid          = guid;
    }
}

define behavior for ZXI_I_PartnerRoleTP alias PartnerRole
persistent table zxipartnerrole
lock dependent by _Partner
authorization dependent by _Partner
etag master TimeStamp
{
  field ( numbering : managed ) Uuid;
  field ( readonly ) Uuid, ParentUuid, TimeStamp;

  update;
  delete;

  association _Partner;

  mapping for zxipartnerrole
    {
      Partner     = PARTNER;
      PartnerRole = PARTNERROLE;
      TimeStamp   = TIMESTAMP;
      Uuid        = GUID;
      ParentUuid  = PARENT_GUID;
    }
}