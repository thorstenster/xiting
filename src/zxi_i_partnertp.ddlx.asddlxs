@Metadata.layer: #PARTNER
@UI.headerInfo: {
    typeName: 'Partner',
    typeNamePlural: 'Partner',
    title: {
        type: #STANDARD,
        label: 'Partner',
        value: 'Partner'
    },
    description: {
        type: #STANDARD,
        label: 'Partner',
        value: 'FullName'
    }
}
@UI.presentationVariant: [{
    sortOrder: [{
        by: 'Partner',
        direction: #ASC
    }]
}]
@UI.textArrangement: #TEXT_ONLY
annotate view ZXI_I_PartnerTP with
{
  @UI.facet: [
  {   purpose: #HEADER,
      position: 10,
      type: #FIELDGROUP_REFERENCE,
      targetQualifier: 'admin1'
  },
  {   purpose: #HEADER,
      position: 20,
      type: #FIELDGROUP_REFERENCE,
      targetQualifier: 'admin2'
  },
  {   purpose: #HEADER,
      position: 30,
      type: #FIELDGROUP_REFERENCE,
      targetQualifier: 'admin3'
  },
  {
      purpose: #STANDARD,
      position: 10,
      type: #IDENTIFICATION_REFERENCE
  },
  {
      purpose: #STANDARD,
      position: 20,
      targetElement: '_PartnerRoles',
      type: #LINEITEM_REFERENCE
  }]
  @UI.selectionField: [{ position: 10 }]
  @UI.lineItem:       [{ position: 10 },
                       { position: 70, type: #FOR_ACTION, dataAction: 'partnerLock', label: 'Partner sperren', inline: false, importance: #HIGH },
                       { position: 80, type: #FOR_ACTION, dataAction: 'partnerUnlock', label: 'Partner entsperren', inline: false, importance: #HIGH }]
  @UI.identification: [{ position: 10, type: #FOR_ACTION, dataAction: 'partnerLock', label: 'Partner sperren' },
                       { position: 15, type: #FOR_ACTION, dataAction: 'partnerUnlock', label: 'Partner entsperren' }]
  @UI.textArrangement: #TEXT_SEPARATE
  Partner;
  @UI.lineItem:       [{ position: 20 }]
  @UI.identification: [{ position: 20 }]
  Title;
  @UI.lineItem:       [{ position: 30 }]
  @UI.identification: [{ position: 30 }]
  AcademicTitle;
  @UI.selectionField: [{ position: 50 }]
  @UI.identification: [{ position: 40 }]
  FirstName;
  @UI.selectionField: [{ position: 30 }]
  @UI.identification: [{ position: 60 }]
  LastName;
  @UI.lineItem:       [{ position: 40 }]
  FullName;
  @UI.selectionField: [{ position: 65 }]
  @UI.lineItem:       [{ position: 65 }]
  @UI.identification: [{ position: 65 }]
  IsLocked;
  @UI.textArrangement: #TEXT_ONLY
  @UI.fieldGroup:     [{ position: 10, qualifier: 'admin1' }]
  CreatedBy;
  @UI.fieldGroup:     [{ position: 20, qualifier: 'admin1' }]
  CreatedAtDate;
  @UI.fieldGroup:     [{ position: 30, qualifier: 'admin1' }]
  CreatedAtTime;
  @UI.textArrangement: #TEXT_ONLY
  @UI.fieldGroup:     [{ position: 10, qualifier: 'admin2' }]
  ChangedBy;
  @UI.fieldGroup:     [{ position: 20, qualifier: 'admin2' }]
  ChangedAtDate;
  @UI.fieldGroup:     [{ position: 30, qualifier: 'admin2' }]
  ChangedAtTime;
  @UI.hidden: true
  Uuid;
}